#version 460 core

#extension GL_ARB_bindless_texture: require

layout(local_size_x = 32) in;

struct Particle
{
  vec3 position;
  float density;
  vec3 velocity;
  float pressure;
};

layout(binding = 0, std430) restrict buffer particleBuf1
{
  Particle particles[];
};

layout(location = 0, r32ui, bindless_image) uniform restrict uimage3D grid;
layout(location = 1) uniform vec3 invCellSize;
layout(location = 2) uniform vec3 gridOrigin;
layout(location = 3) uniform uint particleCount;
layout(location = 4) uniform vec3 gridSize;
layout(location = 5) uniform float dt;

const float SAFE_BOUNDS = 0.001;

void main()
{
  const uint particleId = gl_GlobalInvocationID.x;

  if (particleId >= particleCount)
  {
    return;
  }

  const Particle particle = particles[particleId];

  vec3 newVelo = particle.velocity;
  vec3 newPos = particle.position + newVelo * dt;

  const float wallDamping = 0.5;
  const vec3 boundsL = gridOrigin + SAFE_BOUNDS;
  const vec3 boundsH = gridOrigin + gridSize - SAFE_BOUNDS;

  if (newPos.x < boundsL.x) { newVelo.x *= -wallDamping; newPos.x = boundsL.x; }
  if (newPos.x > boundsH.x) { newVelo.x *= -wallDamping; newPos.x = boundsH.x; }
  if (newPos.y < boundsL.y) { newVelo.y *= -wallDamping; newPos.y = boundsL.y; }
  if (newPos.y > boundsH.y) { newVelo.y *= -wallDamping; newPos.y = boundsH.y; }
  if (newPos.z < boundsL.z) { newVelo.z *= -wallDamping; newPos.z = boundsL.z; }
  if (newPos.z > boundsH.z) { newVelo.z *= -wallDamping; newPos.z = boundsH.z; }

  particles[particleId].velocity = newVelo;
  particles[particleId].position = newPos;

  const ivec3 voxelCoord = ivec3(invCellSize * (newPos - gridOrigin));

  imageAtomicAdd(grid, voxelCoord, 1);
}
